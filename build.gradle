import org.openapitools.generator.gradle.plugin.tasks.GenerateTask
import org.openapitools.generator.gradle.plugin.tasks.ValidateTask

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.openapi.generator' version '7.9.0'
}

group = 'info.touret.apifirst'
version = '0.0.1-SNAPSHOT'
apply plugin: 'org.openapi.generator'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

    testImplementation 'io.github.microcks:microcks-testcontainers:0.2.9'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'

}

tasks.named('test') {
    useJUnitPlatform()
}
openApiValidate {
    inputSpec = "$projectDir${openapi_path}".toString()
    recommend = true
}
openApiGenerate {
    generatorName = "spring"
    library = "spring-boot"
    inputSpec = "$projectDir${openapi_path}".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = "info.touret.apifirst.sample.generated"
    invokerPackage = "info.touret.apifirst.sample.generated.invoker"
    modelPackage = "info.touret.apifirst.sample.generated"
    configOptions = [
            dateLibrary          : "java8",
            java8                : "true",
            openApiNullable      : "false",
            documentationProvider: "springdoc",
            useBeanValidation    : "true",
            interfaceOnly        : "true",
            useSpringBoot3       : "true",
            generateConstructorWithAllArgs : "true"
    ]
}
sourceSets.main.java.srcDirs += "$buildDir/generated/src/main/java".toString()
compileJava.dependsOn 'openApiGenerate'
